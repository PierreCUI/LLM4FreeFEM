// Start time record.
real timeStart = clock();

// Load necessary packages.
load "iovtk";

// Define all constants.
real k = 1.0;
real c = 3.0;
real f = 4.0;

int nn = 20;
real timeStepSize = 0.1;
real finalTime = 10.0;

// Define the mesh.
int[int] labels = [1, 2, 3, 4];
mesh Th = square(nn, nn, label=labels);

// Define the FE spaces.
fespace VhU(Th, P1);
fespace VhdeltaU(Th, P1);

VhU u = 0.0;
VhU uold = 0.0;

VhU incu;
VhdeltaU deltau;

// Include the files from ./include folder.
include "include/heatequationNewton.edp"
include "include/heatequationResidual.edp"

// Time loop.
for (real t = 0.0; t < finalTime - 0.5 * timeStepSize; t += timeStepSize)
{
	uold = u;

	matrix A = heatequationNewton(VhU, VhdeltaU);
	set(A, solver=sparsesolver);

	real[int] rhs = heatequationResidual(0, VhdeltaU);

	real[int] sol = A^-1 * rhs;

	[incu[]] = sol;
	u[] -= incu[];
}

// Save the results.
string DataNameTh = "u";
int[int] OrderTh = [1];
savevtk("results/heatTransient.vtu", Th, u, dataname=DataNameTh, order=OrderTh);

cout << "Total Runtime:" << clock() - timeStart << endl;